;----------------------------------------------------------------
; 生成文本 / 按钮 HTML
;----------------------------------------------------------------
@get_text_html(text, style=0, color="", tooltip="", button="", bcolor="", font="")
#FUNCTIONS
    #DIMS text
    #DIM style
    #DIMS color
    #DIMS tooltip
    #DIMS button
    #DIMS bcolor
    #DIMS font
    #DIMS html
    SIF GETBIT(style, 0)
        text '= @"<b>%text%</b>"
    SIF GETBIT(style, 1)
        text '= @"<i>%text%</i>"
    SIF GETBIT(style, 2)
        text '= @"<s>%text%</s>"
    SIF GETBIT(style, 3)
        text '= @"<u>%text%</u>"
    SIF STRFINDU(color, "#") != 0 && COLOR_FROMNAME(color) == -1
        color '= @"#%TOSTR(GETCOLOR(), "X6")%"
    SIF STRFINDU(bcolor, "#") != 0 && COLOR_FROMNAME(bcolor) == -1
        bcolor '= @"#%TOSTR(GETFOCUSCOLOR(), "X6")%"
    SIF !CHKFONT(font)
        font '= GETFONT()
    SIF button == "" && tooltip == ""
        RETURNF @"<font face='%font%' color='%color%' bcolor='%bcolor%'>%text%</font>"
    html '= button == "" ? "<nonbutton" # @"<button value='%button%'"
    html += @" title='%tooltip%'>"
    html += @"<font face='%font%' color='%color%' bcolor='%bcolor%'>%text%</font>"
    html += button == "" ? "</nonbutton>" # "</button>"
    RETURNF html